group 'de.mintware.barcode_scan'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.5.10'
    ext.protobuf_version = '0.9.3'
    repositories {
        mavenCentral()
        google()
        // jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobuf_version"
    }
}

rootProject.allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.proto.srcDirs += '../protos'
        main.assets.srcDirs = ['assets', 'src/main/assets', 'src/main/assets/']
    }
    defaultConfig {
        minSdkVersion 18
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}


protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        if (project.hasProperty('protoc_platform')) {
            artifact = "com.google.protobuf:protoc:3.11.4:${protoc_platform}"
        } else {
            artifact = "com.google.protobuf:protoc:3.11.4"
        }
        // artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            // artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
            if (project.hasProperty('protoc_platform')) {
                artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0:${protoc_platform}"
            } else {
                artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
            }
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite { }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    api 'com.google.android.material:material:1.1.0'
}
